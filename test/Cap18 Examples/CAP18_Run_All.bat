@echo off
::=============================================================================|
::  FILENAME     CAP18_run_all.bat                                             |
::  AGENCY       TxDOT   [Texas Department of Transportation]                  |
::  SYSTEM       CAP18                                                         |
::  LANGUAGE     WINDOWS COMMAND LANGUAGE                                      |
::  SUBJECT      Process all CAP18 input data files within a folder.           |
::-----------------------------------------------------------------------------|
::     Designed and created explicitly for the needs of TxDOT.                 |
::-----------------------------------------------------------------------------|
::!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!   !
::!! ATTENTION: THIS WINDOWS BATCH FILE IS A SOFTWARE UTILITY TOOL THAT    !   !
::!!            IS NOT INTENDED FOR USER MODIFICATION.                     !   !
::!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!   !
::-----------------------------------------------------------------------------|
::  DESCRIPTION                                                                |
::                                                                             |
::  The purpose of this Windows batch program is to process all CAP18          |
::  input data files that reside in the same folder.  All files with a         |
::  ".dat" file extension are assumed to be CAP18 input data files.            |
::  When this batch file is run, all ".dat" files in the folder where the      |
::  batch file resides will be located, and the number of files located will   |
::  be reported.  The CAP18 program will be invoked to process each ".dat"     |
::  file.  All output files generated by CAP18 will be created in the same     |
::  folder as the input files.                                                 |
::-----------------------------------------------------------------------------|
::  HOW TO USE THIS PROGRAM                                                    |
::                                                                             |
::  The following steps describe how to use this Windows batch program to      |
::  process all CAP18 input files that reside in a specific folder:            |
::                                                                             |
::  1. Place all CAP18 input files that you wish to process in a common        |
::     folder. All files with a ".dat" file extension will be assumed          |
::     to be CAP18 input data files.  The folder should not contain            |
::     any other ".dat" files that you do not wish to process.                 |
::  2. Place a copy of this batch file, CAP18_run_all.bat, in the same         |
::     folder as the input files.                                              |
::  3. Start the CAP18_run_all.bat batch program. You can do this by           |
::     double-clicking the file in an Explores window.                         |
::                                                                             |
::     The program will open a command window, report the number of            |
::     input files located, including user instructions, and then pause to     |
::     wait for a response from the user. To initiate the processing of all    |
::     input files you must respond by entering the letter "y" and then        |
::     press the Enter key.                                                    |
::                                                                             |
::     The program will process each ".dat" file and will pause when           |
::     processing is finished. Press the Enter key to end the program.         |
::                                                                             |
::     All output files generated by the CAP18 program will be created in the  |
::     same folder as the input files.                                         |
::                                                                             |
::-----------------------------------------------------------------------------+
::  HISTORY      Date:        Name:               Description:                 |
::               2012-May-12  Danny A. Bradley    Created.		       |
::----------------------------------------------------------------------------*/

::------------------------------------------------------------------------------
::                                                                           
:: Clear the screen & Display banner
::
CLS
echo.
echo   -------------------------------------------------------------------------
echo   Program: CAP18_run_all
echo.
echo   This program will process all CAP18 input data files that reside in the
echo   following folder:
echo.
echo   %CD%  
echo.
echo   Any file with a ".dat" file extension is assumed to be a CAP18 input file.
echo   -------------------------------------------------------------------------
echo.
::
::  Determine data file count
::
SET /A FN=0
for %%F in (*.dat) do SET /a FN=FN+1 
::
::  Report the count
::
IF NOT "%FN%" == "0" goto REPORT
echo   The folder contains no CAP18 input data files.
goto DONE
:REPORT
echo   A total of %FN% CAP18 input data files have been located.
echo.
::
::  Query user: Continue or exit?
::
SET CHOICE=
SET /P CHOICE=  Press 'Y' plus ENTER to Process files or just ENTER to Exit:  
:: IF NOT '%CHOICE%' == '' goto RunFiles
IF '%CHOICE%' == 'Y' goto RunFiles
IF '%CHOICE%' == 'y' goto RunFiles
echo.
echo   No CAP18 data files processed.
goto done
::
::  Run CAP18 with each and every data file in the folder
::
:RunFiles
for %%F in (*.dat) do "CAP18.exe" "%CD%\%%F" 
::
:: Perform Garbage collection and leave
::
:DONE
echo.
echo   CAP18_RUN_ALL DONE.
echo.
pause







